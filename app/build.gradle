apply plugin: 'com.android.application'

android {
    compileSdkVersion 28
    flavorDimensions "default"

    defaultConfig {
        applicationId "jp.co.sakamoto.androidproject"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 1
        versionName "1.0.0"

        buildConfigField 'String', 'URL_BASE', '"http://your.url.base/"'
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        targetCompatibility JavaVersion.VERSION_1_8
        sourceCompatibility JavaVersion.VERSION_1_8
    }

    dataBinding {
        enabled = true
    }

    productFlavors {
        develop {
            applicationIdSuffix ".dev"
            buildConfigField 'String', 'URL_BASE', '"http://your.url.base.dev/"'
        }
        staging {
            applicationIdSuffix ".stg"
            buildConfigField 'String', 'URL_BASE', '"http://your.url.base.stg/"'
        }
        product {}
    }
    // RxJavaとRxJava2が混在している場合に必要
    packagingOptions {
        exclude 'META-INF/rxjava.properties'
    }
    //TODO: Deep Linkを使用しないならコメントアウト解除
//    lintOptions {
//        disable 'GoogleAppIndexingWarning'
//        baseline file("lint-baseline.xml")
//    }
}

ext.support_version = "28.0.0"
ext.play_service_version = "16.1.0"
ext.play_service_location_version = "16.0.0"
ext.dagger_version = "2.17"
ext.retrofit_version = "2.4.0"
ext.orma_version = "5.0.2"

dependencies {
    // support lib
    implementation "com.android.support:support-v4:$support_version"
    implementation "com.android.support:appcompat-v7:$support_version"
    implementation "com.android.support:recyclerview-v7:$support_version"
    implementation "com.android.support:design:$support_version"
    implementation "com.android.support.constraint:constraint-layout:1.1.3"

    // play service
    implementation "com.google.android.gms:play-services-gcm:$play_service_version"
    implementation "com.google.android.gms:play-services-location:$play_service_location_version"

    // rxjava
    implementation "io.reactivex.rxjava2:rxjava:2.2.6"
    implementation "com.jakewharton.rxbinding:rxbinding-recyclerview-v7:0.4.0"

    // dagger
    implementation "com.google.dagger:dagger:$dagger_version"
    implementation "com.google.dagger:dagger-android:$dagger_version"
    implementation "com.google.dagger:dagger-android-support:$dagger_version"
    annotationProcessor "com.google.dagger:dagger-compiler:$dagger_version"
    annotationProcessor "com.google.dagger:dagger-android-processor:$dagger_version"

    // retrofit2
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-scalars:$retrofit_version"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"

    // orma
    implementation "com.github.maskarade.android.orma:orma:$orma_version"
    annotationProcessor "com.github.maskarade.android.orma:orma-processor:$orma_version"

    // gif drawable
    implementation "pl.droidsonroids.gif:android-gif-drawable:1.2.11"

    // zxing
    implementation "com.google.zxing:core:3.3.0"

    // unit test
    androidTestImplementation("com.android.support.test.espresso:espresso-core:2.2.2", {
        exclude group: "com.android.support", module: "support-annotations"
    })
    androidTestImplementation "junit:junit:4.12"

    // local lib
    implementation fileTree(include: ['*.jar'], dir: 'libs')
}
//apply plugin: 'com.google.gms.google-services'

